plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Huawei") and !(getGradle().getStartParameter().getTaskRequests().toString().contains("Google"))) {
    apply plugin: 'com.huawei.agconnect'
} else  if (getGradle().getStartParameter().getTaskRequests().toString().contains("Google") and !(getGradle().getStartParameter().getTaskRequests().toString().contains("Huawei"))){
    apply plugin: 'com.google.gms.google-services'
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    namespace 'com.exolve.voicedemo'
    compileSdk 35

    signingConfigs {
        debug {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }

        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "com.exolve"
        minSdkVersion 26
        targetSdk 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    // Specifies  flavor dimensions
    flavorDimensions "platform"

    productFlavors {
        google {
            dimension "platform"
            applicationIdSuffix ".demoapp"
            buildConfigField "String", "PLATFORM", "\"android\""
            resValue "string", "app_name", "VoiceDemo"
        }
        huawei {
            dimension "platform"
            applicationIdSuffix ".demoapp"
            buildConfigField "String", "PLATFORM", "\"huawei\""
            resValue "string", "app_name", "VoiceDemo"
        }
    }

    sourceSets {
        google {
            kotlin.srcDirs += ['src/messaging/google/java']
        }
        huawei {
            kotlin.srcDirs += ['src/messaging/huawei/java']
        }
    }

    buildTypes {
        debug {
            productFlavors.google.signingConfig signingConfigs.debug
            productFlavors.huawei.signingConfig signingConfigs.debug
        }
        release {
            productFlavors.google.signingConfig signingConfigs.config
            productFlavors.huawei.signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.2"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    lint {
        baseline file('lint-baseline.xml')
    }
}

dependencies {
    implementation 'com.exolve:voicesdk:1.7.1'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation "com.google.accompanist:accompanist-pager:0.28.0" // Pager
    implementation "com.google.accompanist:accompanist-pager-indicators:0.28.0" // Pager Indicators
    implementation "androidx.test.espresso:espresso-core:3.5.1"
    implementation "androidx.test:runner:1.5.2"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.activity:activity-ktx:1.9.2"
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4")
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.4.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation "androidx.constraintlayout:constraintlayout:2.2.0-alpha05"
    implementation "androidx.navigation:navigation-compose:2.6.0-alpha04"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.1.0-alpha05"
    implementation "androidx.compose.ui:ui-tooling:1.4.0-alpha04"
    implementation "androidx.compose.material3:material3:1.2.1"
    implementation "androidx.compose.material3:material3-window-size-class:1.2.1"
    implementation "androidx.compose.material3:material3-adaptive-navigation-suite:1.0.0-alpha05"
    implementation 'com.google.android.gms:play-services-tasks:18.0.2'
    googleImplementation platform('com.google.firebase:firebase-bom:33.1.1')
    googleImplementation 'com.google.firebase:firebase-messaging'
    googleImplementation 'com.google.firebase:firebase-analytics-ktx'
    huaweiImplementation 'com.huawei.hms:push:6.3.0.300'
    implementation "androidx.core:core-ktx:1.9.0"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.3.3"
    debugImplementation "androidx.compose.ui:ui-tooling:1.3.2"
    debugImplementation "androidx.compose.ui:ui-test-manifest:1.3.2"
}
